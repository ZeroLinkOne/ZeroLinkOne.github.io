---
layout: post
comments: true
categories: 创意生活
---

## 一、现象
网购的桶装水抽水器，每次抽水刚接半壶就自动停机，用着特别闹心，早就想把这个问题解决掉了。

  ![抽水器订单](https://github.com/ZeroLinkOne/ZeroLinkOne.github.io/blob/master/images/WaterPumpDeal.jpg?raw=true)

## 二、改装
拆开抽水器,里面长这样
  
  ![旧抽水器](https://github.com/ZeroLinkOne/ZeroLinkOne.github.io/blob/master/images/WaterPumpOld.jpg?raw=true)

一颗 8 脚单片机、一枚 TP4054 充电芯片，再搭配一个按键，整体电路结构十分简洁。

手头正好有 PY32F002AL15S6TU 单片机，引脚大多能对应上，索性用 Arduino 写了版程序，烧录、焊接一气呵成，一次就成功了。

电池升级为两节 18650，容量提升至 5000mAh，原 500mAh 的 14500 电池弃用

低功耗部分基于 Regimantas 编写的 PY32LowPower 库(https://github.com/regimantas/PY32LowPower)做了适配修改，让它能兼容 Air001 使用。代码如下:

    #include "Py32fDeepSleep.h"

    #define BTNMODE INPUT_PULLUP
    #define BTNDOWN LOW
    #define OUTTIME 270
    #define OUTIDLE HIGH

    Py32fDeepSleep xSleep;

    #define BTNN PA4
    #define LEDD PA1
    #define VOUT PA3

    int bStat = LOW;

    void setup() {
      xSleep.begin();  // Initialize low power settings
      pinMode(BTNN, BTNMODE);
      pinMode(LEDD, OUTPUT);
      pinMode(VOUT, OUTPUT);
      digitalWrite(LEDD, LOW);  // Ensure LED is off initially
      digitalWrite(VOUT, LOW);
      analogReadResolution(12);
      delay(500);
      bStat = digitalRead(BTNN);
    }

    int ss = 0;
    bool led = false;
    int half = 1;
    unsigned long tick = 0;
    unsigned long mTick = 0;

    void loop() {
      bool btnDown = false;
      if (bStat != digitalRead(BTNN)) {  // 按键发生变化
        bStat = digitalRead(BTNN);
        if (bStat == BTNDOWN) {  // 按键被按下
          btnDown = true;
        }
      }
      switch (ss) {
        case 0:
          digitalWrite(LEDD, LOW);
          digitalWrite(VOUT, LOW);
          xSleep.deepSleep(100);  // 低功耗模式
          if (btnDown) {            // 按键被按下
            int vref = analogReadVref();
            if (vref < 3300) {
              for (int i = 0; i < 5; i++) {
                digitalWrite(LEDD, HIGH);
                delay(500);
                digitalWrite(LEDD, LOW);
                delay(500);
              }
              break;
            }
            ss = 1;
            half = 1;
            tick = millis();
            mTick = OUTTIME * 1000 * vref / 4200;
          }
          break;
        case 1:
          digitalWrite(VOUT, HIGH);
          led = (half == 2 && ((millis() - tick) % 1000) < 500);
          digitalWrite(LEDD, led ? LOW : HIGH);
          delay(100);
          if (millis() - tick < 1000 && btnDown) {
            half = 2;
          }
          if (millis() - tick > 1000 && btnDown) {
            mTick = 0;
          }
          if (millis() - tick > mTick/half) {
            ss = 0;
          }
          break;
        default:
          ss = 0;
          break;
      }
    }

单击出水 6L，适配大桶转小桶；双击出水 3L，专为烧水壶设计。电压低于 3.3V 时指示灯闪烁，同时禁止抽水。

## 三、打板

使用一段时间后发现 TP4054 充电速率过慢，干脆自主画板打样，将充电芯片升级为 TP4056，同时增设调试接口，方便后续调试测试。

板面设计借鉴自 rockee 开源的阳光水泵系列板卡（项目链接：https://oshwhub.com/rockee/sunshine-water-pump），在保留原方案核心优势的基础上，进行了集成化优化 —— 将核心板、扩展板与烧录功能整合于单张 PCB,最终效果如下:

  ![新抽水器](https://github.com/ZeroLinkOne/ZeroLinkOne.github.io/blob/master/images/WaterPumpNew.png?raw=true)

经焊接及功能测试验证，设备当前运行稳定。
